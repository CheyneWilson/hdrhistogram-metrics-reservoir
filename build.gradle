plugins {
  id 'java'
  id 'maven-publish'
  id 'me.champeau.gradle.jmh' version '0.1.3'
  id 'net.researchgate.release' version '2.3.4'
  id 'com.jfrog.bintray' version '1.5'
}

group 'org.mpierce.metrics.reservoir'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

defaultTasks 'build'

repositories {
  jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'com.google.code.findbugs:jsr305:3.0.1'
  compile 'io.dropwizard.metrics:metrics-core:3.1.2'
  compile 'org.hdrhistogram:HdrHistogram:2.1.8'
}

task sourceJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
}

publishing {
  publications {
    bintray(MavenPublication) {
      from components.java
      groupId project.group
      artifactId project.name
      version project.version

      artifact sourceJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier 'javadoc'
      }
    }
  }
}

release {
  tagTemplate = 'v$version'
}

bintray {
  user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
  key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
  publications = ['bintray']

  pkg {
    repo = 'maven'
    licenses = ['Copyfree']
    vcsUrl = 'https://bitbucket.org/marshallpierce/hdrhistogram-metrics-reservoir'
    name = 'org.mpierce.metrics.reservoir:hdrhistogram-metrics-reservoir'

    version {
      name = project.version
      released = new Date()
      vcsTag = 'v' + project.version
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.9'
}

