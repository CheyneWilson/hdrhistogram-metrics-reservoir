plugins {
  id 'java'
  id 'maven'
  id 'signing'
  id 'me.champeau.gradle.jmh' version '0.1.3'
  id 'net.researchgate.release' version '2.0.2'
}

group 'org.mpierce.metrics.reservoir'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

defaultTasks 'build'

repositories {
  jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'com.google.code.findbugs:jsr305:3.0.0'
  compile 'io.dropwizard.metrics:metrics-core:3.1.1'
  compile 'org.hdrhistogram:HdrHistogram:2.1.6'
}

task sourceJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allJava
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

artifacts {
  // register in 'archives' config so they will be signed
  archives sourceJar
  archives javadocJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

release {
  tagPrefix = 'v'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { deployment -> signing.signPom(deployment) }

      repository(url: isReleaseVersion ?
          "https://oss.sonatype.org/service/local/staging/deploy/maven2/" :
          "https://oss.sonatype.org/content/repositories/snapshots/") {
        def u = project.hasProperty('sonatypeUsername') ? sonatypeUsername : 'FIXME'
        def p = project.hasProperty('sonatypePassword') ? sonatypePassword : 'FIXME'
        authentication(userName: u, password: p)
      }

      pom.project {
        name rootProject.name
        packaging 'jar'
        description 'Reservoir implementation backed by HdrHistogram'
        url 'https://bitbucket.org/marshallpierce/hdrhistogram-metrics-reservoir'

        scm {
          url 'scm:git@bitbucket.org:marshallpierce/hdrhistogram-metrics-reservoir.git'
          connection 'scm:git@bitbucket.org:marshallpierce/hdrhistogram-metrics-reservoir.git'
          developerConnection 'scm:git@bitbucket.org:marshallpierce/hdrhistogram-metrics-reservoir.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'marshallpierce'
            name 'Marshall Pierce'
          }
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}

